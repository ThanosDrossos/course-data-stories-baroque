PREFIX sc: <http://purl.org/science/owl/sciencecommons/>
PREFIX ct: <http://data.linkedct.org/resource/linkedct/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nfdicore: <https://nfdi.fiz-karlsruhe.de/ontology/>
PREFIX cto: <https://nfdi4culture.de/ontology#>
PREFIX partitura: <https://nocodb.nfdi4culture.de/dashboard/#/nc/pxjfen9oerev7k9/mj3w842gefzot7u?rowId=>

SELECT
  (?partituraLabel as ?opera)
  (?composerLabel as ?composer)
  (SAMPLE(?locationLabel) as ?locationLabel)
  (SAMPLE(?year) as ?year)
  (SAMPLE(?rismItem) as ?rismExample)
  (SAMPLE(?partitura) as ?partituraExample)
WHERE {
  SERVICE <https://lod.academy/dhi-rom/data/partitura/sparql> {
    SELECT
      ?partitura
      ?partituraLabel
      ?composerLabel
      ?rismItem
      ?locationLabel
      ?year
    {
      ?partitura a cto:Item .
      ?partitura rdfs:label ?partituraLabel .

      ?partitura schema:composer ?composer .
      ?composer rdfs:label ?composerLabel .
      ?partitura cto:relatedPerson <https://rism.online/people/17950> .

      ?partitura cto:relatedLocation ?location .
      ?location rdfs:label ?locationLabel .

      ?partitura cto:relatedEvent ?event .
      ?event nfdicore:startDate ?year .

      ?partitura cto:relatedItem ?rismItem .
    }
  }

  # with a relation to Pietro Metastasio
  ?rismItem cto:relatedPerson <https://rism.online/people/97823> .
}
GROUP BY ?partituraLabel ?composerLabel
ORDER BY ?partituraLabel ?composerLabel